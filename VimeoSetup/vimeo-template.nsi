# ====================== Macro ==============================
!define PRODUCT_NAME           "vimeo"
!define EXE_NAME               "vimeo.exe"
!define PRODUCT_VERSION        "1.0.0.1"
!define PRODUCT_PUBLISHER      "vimeo"
!define PRODUCT_LEGAL          "Copyright (C) 1999-2014 vimeo, All Rights Reserved"


!include "LogicLib.nsh"
!include "nsDialogs.nsh"


# ===================== Setup Info =============================
VIProductVersion                    "${PRODUCT_VERSION}"
VIAddVersionKey "ProductVersion"    "${PRODUCT_VERSION}"
VIAddVersionKey "ProductName"       "${PRODUCT_NAME}"
VIAddVersionKey "CompanyName"       "${PRODUCT_PUBLISHER}"
VIAddVersionKey "FileVersion"       "${PRODUCT_VERSION}"
VIAddVersionKey "InternalName"      "${EXE_NAME}"
VIAddVersionKey "FileDescription"   "${PRODUCT_NAME}"
VIAddVersionKey "LegalCopyright"    "${PRODUCT_LEGAL}"

# ==================== NSIS Attribute ================================

Unicode True
#SetCompressor zlib
Name "${PRODUCT_NAME}"
OutFile "vimeo-setup.exe"
Icon              "vimeo.ico"
UninstallIcon     "vimeo.ico"

# UAC
# RequestExecutionLevel none|user|highest|admin
RequestExecutionLevel admin


# Custom Install Page
Page custom QtUiPage


# Show Uninstall details
UninstPage instfiles

# ======================= Qt Page =========================
Function QtUiPage
	MessageBox MB_ICONINFORMATION|MB_OK "[Debug Info] NSIS Plugin Dir: $PLUGINSDIR" /SD IDOK
	
	GetFunctionAddress $0 OnStartExtractFiles
	nsQtPlugin::BindInstallEventToNsisFunc "START_EXTRACT_FILES" $0
	
	GetFunctionAddress $0 OnUserCancelInstall
	nsQtPlugin::BindInstallEventToNsisFunc "USER_CANCEL" $0
	
    nsQtPlugin::ShowSetupUI $PLUGINSDIR
FunctionEnd


Function OnStartExtractFiles
	nsQtPlugin::GetInstallDirectory
	Pop $0
	StrCmp $0 "" InstallAbort 0
    StrCpy $INSTDIR "$0"
	MessageBox MB_ICONINFORMATION|MB_OK "[Debug Info] Install Dir: $0"  /SD IDOK
	
	SetOutPath $INSTDIR
  
    GetFunctionAddress $0 ___ExtractFiles
    nsQtPlugin::BackgroundRun $0
		

InstallAbort:
FunctionEnd


Function OnUserCancelInstall
	MessageBox MB_ICONINFORMATION|MB_OK "[Debug Info] OnUserCancelInstall"  /SD IDOK
	Abort
FunctionEnd


# don't edit this function
# this function generated by python script
Function ___ExtractFiles
	Call OnAfterExtractFiles
FunctionEnd


Function OnAfterExtractFiles
	Call CreateUninstall
	Call CreateShortcut
FunctionEnd


Function CreateShortcut
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk" "$INSTDIR\Bin\${EXE_NAME}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall ${PRODUCT_NAME}.lnk" "$INSTDIR\uninst.exe"
  CreateShortCut "$DESKTOP\${PRODUCT_NAME}.lnk" "$INSTDIR\Bin\${EXE_NAME}"
  SetShellVarContext current
FunctionEnd


Function CreateUninstall
	WriteUninstaller "$INSTDIR\uninst.exe"
	
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "DisplayName" "${PRODUCT_NAME}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "UninstallString" "$INSTDIR\uninst.exe"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "DisplayIcon" "$INSTDIR\${EXE_NAME}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "Publisher" "$INSTDIR\${PRODUCT_PUBLISHER}"
FunctionEnd

# Add an empty section, avoid compile error.
Section "None"
SectionEnd


# Uninstall Section
Section "Uninstall"

  SetShellVarContext all
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall ${PRODUCT_NAME}.lnk"
  RMDir "$SMPROGRAMS\${PRODUCT_NAME}\"
  Delete "$DESKTOP\${PRODUCT_NAME}.lnk"
  SetShellVarContext current
  
  SetOutPath "$INSTDIR"

  ; Delete installed files
  Delete "$INSTDIR\*.*"

  SetOutPath "$DESKTOP"

  RMDir /r "$INSTDIR"
  RMDir "$INSTDIR"
  
  SetAutoClose true
SectionEnd



Function .onInit
	# makesure plugin directory exist
	InitPluginsDir
	
	# place Qt dlls to plugin directory
    File /oname=$PLUGINSDIR\Qt5Cored.dll "$%QTDIR%\bin\Qt5Cored.dll"
	File /oname=$PLUGINSDIR\Qt5Guid.dll "$%QTDIR%\bin\Qt5Guid.dll"
	File /oname=$PLUGINSDIR\Qt5Widgetsd.dll "$%QTDIR%\bin\Qt5Widgetsd.dll"
	File /oname=$PLUGINSDIR\Qt5Svgd.dll "$%QTDIR%\bin\Qt5Svgd.dll"
	
	CreateDirectory $PLUGINSDIR\platforms
	File /oname=$PLUGINSDIR\platforms\qwindowsd.dll "$%QTDIR%\plugins\platforms\qwindowsd.dll"
	
	CreateDirectory $PLUGINSDIR\styles
	File /oname=$PLUGINSDIR\styles\qwindowsvistastyled.dll "$%QTDIR%\plugins\styles\qwindowsvistastyled.dll"
	
	CreateDirectory $PLUGINSDIR\imageformats
	File /oname=$PLUGINSDIR\imageformats\qgifd.dll "$%QTDIR%\plugins\imageformats\qgifd.dll"
	File /oname=$PLUGINSDIR\imageformats\qicnsd.dll "$%QTDIR%\plugins\imageformats\qicnsd.dll"
	File /oname=$PLUGINSDIR\imageformats\qicod.dll "$%QTDIR%\plugins\imageformats\qicod.dll"
	File /oname=$PLUGINSDIR\imageformats\qjpegd.dll "$%QTDIR%\plugins\imageformats\qjpegd.dll"
	File /oname=$PLUGINSDIR\imageformats\qsvgd.dll "$%QTDIR%\plugins\imageformats\qsvgd.dll"
	CreateDirectory $PLUGINSDIR\iconengines
	
	File /oname=$PLUGINSDIR\iconengines\qsvgicond.dll "$%QTDIR%\plugins\iconengines\qsvgicond.dll"
	
	# place vc runtime dlls to plugin directory
	File /oname=$PLUGINSDIR\concrt140d.dll "VCRuntimeDLL\concrt140d.dll"
	File /oname=$PLUGINSDIR\msvcp140d.dll "VCRuntimeDLL\msvcp140d.dll"
	File /oname=$PLUGINSDIR\msvcp140_1d.dll "VCRuntimeDLL\msvcp140_1d.dll"
	File /oname=$PLUGINSDIR\msvcp140_2d.dll "VCRuntimeDLL\msvcp140_2d.dll"
	File /oname=$PLUGINSDIR\ucrtbased.dll "VCRuntimeDLL\ucrtbased.dll"
	File /oname=$PLUGINSDIR\vccorlib140d.dll "VCRuntimeDLL\vccorlib140d.dll"
	File /oname=$PLUGINSDIR\vcruntime140d.dll "VCRuntimeDLL\vcruntime140d.dll"
FunctionEnd



Function .onInstSuccess

FunctionEnd


Function .onInstFailed
    MessageBox MB_ICONQUESTION|MB_YESNO "Install Failed!" /SD IDYES IDYES +2 IDNO +1
FunctionEnd



# Before Uninstall
Function un.onInit
    MessageBox MB_ICONQUESTION|MB_YESNO "Are you sure to uninstall ${PRODUCT_NAME}?" /SD IDYES IDYES +2 IDNO +1
    Abort
FunctionEnd

Function un.onUninstSuccess

FunctionEnd


